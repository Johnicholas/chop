native @nohold _printf();

input (int) IDENTIFIER;
input (int) INTEGER;
input void PLUS;
input void TIMES;
input (int) ASSIGN;
input void PRINT;

data Exp with
  tag NIL;
or
  tag SET with
    var int left;
    var Exp right;
  end
or
  tag MUL with
    var Exp left;
    var Exp right;
  end
or
  tag ADD with
    var Exp left;
    var Exp right;
  end
or
  tag CONST with
    var int value;
  end
or
  tag VAR with
    var int which;
  end
or
  tag CONS with
    var Exp car;
    var Exp cdr;
    var Exp temp;
  end
end

pool Exp[] tree = new Exp.CONS(Exp.NIL(), Exp.NIL(), Exp.NIL());

par do
  loop do
    await PRINT;
    traverse t in &&tree do
      watching *t do
        if t:SET then
          var int x = t:SET.left;
          _printf("X%d = ", x);
          traverse &&t:SET.right;
        else/if t:MUL then
          traverse &&t:MUL.left;
          _printf("*");
          traverse &&t:MUL.right;
        else/if t:ADD then
          traverse &&t:ADD.left;
          _printf("+");
          traverse &&t:ADD.right;
        else/if t:CONST then
          _printf("%d", t:CONST.value);
        else/if t:VAR then
          _printf("X%d", t:VAR.which);
        else/if t:CONS then
          traverse &&t:CONS.car;
          _printf("\n");
          traverse &&t:CONS.cdr;
        else
          _printf("?");
        end
      end
    end
    _printf("\n");
  end
with
  loop do
    var int x = await IDENTIFIER;
    _printf("Saw Identifier: %d\n", x);
    tree.CONS.temp = new Exp.CONS(Exp.NIL(), Exp.VAR(x), Exp.NIL());
    tree.CONS.temp.CONS.car = tree.CONS.car;
    tree.CONS.car = tree.CONS.temp;
  end
with
  loop do
    var int x = await INTEGER;
    _printf("Saw Integer: %d\n", x);
    tree.CONS.temp = new Exp.CONS(Exp.NIL(), Exp.CONST(x), Exp.NIL());
    tree.CONS.temp.CONS.car = tree.CONS.car;
    tree.CONS.car = tree.CONS.temp;
  end
with
  loop do
    await PLUS;
    _printf("Saw PLUS\n");
    tree.CONS.temp = new Exp.CONS(Exp.NIL(), Exp.ADD(Exp.NIL(), Exp.NIL()), Exp.NIL());
    tree.CONS.temp.CONS.cdr.ADD.left = tree.CONS.car.CONS.car.CONS.cdr; // cadr
    tree.CONS.temp.CONS.cdr.ADD.right = tree.CONS.car.CONS.cdr; // car
    tree.CONS.temp.CONS.car = tree.CONS.car.CONS.car.CONS.car; // cddr
    tree.CONS.car = tree.CONS.temp;
  end
with
  loop do
    await TIMES;
    _printf("Saw TIMES\n");
    tree.CONS.temp = new Exp.CONS(Exp.NIL(), Exp.MUL(Exp.NIL(), Exp.NIL()), Exp.NIL());
    tree.CONS.temp.CONS.cdr.MUL.left = tree.CONS.car.CONS.car.CONS.cdr; // cadr
    tree.CONS.temp.CONS.cdr.MUL.right = tree.CONS.car.CONS.cdr; // car
    tree.CONS.temp.CONS.car = tree.CONS.car.CONS.car.CONS.car; // cddr
    tree.CONS.car = tree.CONS.temp;
  end
with
  loop do
    var int x = await ASSIGN;
    _printf("Saw ASSIGN: %d\n", x);
    tree.CONS.temp = new Exp.CONS(Exp.NIL(), Exp.SET(x, Exp.NIL()), Exp.NIL());
    tree.CONS.temp.CONS.cdr.SET.right = tree.CONS.car.CONS.cdr; // car
    tree.CONS.temp.CONS.car = tree.CONS.car.CONS.car; // cdr
    tree.CONS.car = tree.CONS.temp;
  end
end
